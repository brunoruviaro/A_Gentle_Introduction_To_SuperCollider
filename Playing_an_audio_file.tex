\section{Tocando um arquivo de áudio}

Primeiro, você tem que carregar o arquivo de som em um buffer (uma espécie de "contêiner"). O segundo argumento para \texttt{Buffer.read} é o caminho ("path"), entre aspas duplas, indicando onde o seu arquivo de som está salvo. Esse caminho deve apontar para um arquivo WAV ou AIFF no seu computador. Depois que o buffer é carregado, simplesmente use a UGen \texttt{PlayBuf} para tocá-lo de diversas maneiras.

\bigskip
\todo[inline, color=green!40]{ DICA: Um jeito rápido de obter o caminho correto para um arquivo de som no seu computador é arrastar o arquivo para um documento em branco do SuperCollider. O SC fornecerá automaticamente o caminho completo, já entre aspas duplas! }
\bigskip

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Carregar arquivos nos buffers:
~buf1 = Buffer.read(s, "/home/Music/wheels-mono.wav"); // um arquivo de som
~buf2 = Buffer.read(s, "/home/Music/mussorgsky.wav"); // outro arquivo de som

// Tocar:
{PlayBuf.ar(1, ~buf1)}.play; // número de canais e buffer
{PlayBuf.ar(1, ~buf2)}.play;

// Obter informações sobre os arquivos:
[~buf1.bufnum, ~buf1.numChannels, ~buf1.path, ~buf1.numFrames];
[~buf2.bufnum, ~buf2.numChannels, ~buf2.path, ~buf2.numFrames];

// Mudar a velocidade de reprodução com 'rate' 
{PlayBuf.ar(numChannels: 1, bufnum: ~buf1, rate: 2, loop: 1)}.play;
{PlayBuf.ar(1, ~buf1, 0.5, loop: 1)}.play; // tocar na metade da velocidade
{PlayBuf.ar(1, ~buf1, Line.kr(0.5, 2, 10), loop: 1)}.play; // acelerando
{PlayBuf.ar(1, ~buf1, MouseY.kr(0.5, 3), loop: 1)}.play; // controlando com mouse

// Inverter direção (ao contrário)
{PlayBuf.ar(1, ~buf2, -1, loop: 1)}.play; // inverter som
{PlayBuf.ar(1, ~buf2, -0.5, loop: 1)}.play; // tocar o som na metade da velocidade E ao contrário
\end{lstlisting}
