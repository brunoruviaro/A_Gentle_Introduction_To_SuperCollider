\section{Canais de Controle}
\label{sec:control-buses}

Em uma seção anterior deste tutorial, falamos sobre canais de áudio ("Audio Buses") (seção \ref{sec:audiobus}) e o objeto Bus (section \ref{sec:busobject}). Naquele momento, escolhemos deixar de lado o tópico dos Canais de Controle ("Control Buses") para nos focarmos no conceito de de roteamento de áudio.

Canais de controle no SuperCollider são para o roteamento de sinais de controle, não de áudio. Exceto por esta diferença, não há nenhuma outra distinção prática ou conceitual entre canais de áudio de de controle. Você cria e gerencia um canal de controle da mesma maneira que você faz com os canais de áudio, simplesmente usando \texttt{.kr} em vez de \texttt{.ar}. O SuperCollider tem  4096 canais de controle como padrão.

A primeira parte do exemplo abaixo usa um número de canal arbitrário apenas com a finalidade de demonstração. A segunda parte usa o objeto Bus, que é a maneira recomendada de criar canais.


\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Escreva um sinal de controle no canal de controle 55
{Out.kr(55, LFNoise0.kr(1))}.play;
// Leia um sinal de controle do canal 55
{In.kr(55).poll}.play;

// Usando o objeto Bus
~meuCanalDeControle = Bus.control(s, 1);
{Out.kr(~meuCanalDeControle, LFNoise0.kr(5).range(440, 880))}.play;
{SinOsc.ar(freq: In.kr(~meuCanalDeControle))}.play;
\end{lstlisting}

O próximo exemplo mostra um único sinal de controle sendo utilizado para modular dois diferentes sintetizadores ao mesmo tempo. No sintetizador \texttt{Blip}, o sinal de controle e reescalonado para controlar o número de harmónicos entre 1 e 10. No segundo sintetizador, o mesmo sinal de controle é reescalonado para modular a frequência do oscilador \texttt{Pulse}.

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Crie o canal de controle
~meuControle = Bus.control(s, 1);

// Direcione o sinal de controle para o canal
c = {Out.kr(~meuControle, Pulse.kr(freq: MouseX.kr(1, 10), mul: MouseY.kr(0, 1)))}.play;

// Toque os sons que estão sendo controlados
// (mova o mouse para ouvir as mudanças)
(
{
	Blip.ar(
		freq: LFNoise0.kr([1/2, 1/3]).range(50, 60),
		numharm: In.kr(~meuControle).range(1, 10),
		mul: LFTri.kr([1/4, 1/6]).range(0, 0.1))
}.play;

{
	Splay.ar(
		Pulse.ar(
			freq: LFNoise0.kr([1.4, 1, 1/2, 1/3]).range(100, 1000)
			* In.kr(~meuControle).range(0.9, 1.1),
			mul: SinOsc.ar([1/3, 1/2, 1/4, 1/8]).range(0, 0.03))
	)
}.play;
)

// Desligue o sinal de controle para comparar
c.free;
\end{lstlisting}

\subsection{asMap}

No próximo exemplo, o método \texttt{asMap} ("como mapa") é usado para mapear um canal de controle diretamente para um nó de um sintetizador que esteja rodando. Desta maneira, você não precisara sequer de um \texttt{In.kr} na definição do sintetizador.

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Crie um SynthDef
SynthDef("simples", {arg freq = 440; Out.ar(0, SinOsc.ar(freq, mul: 0.2))}).add;
// Crie um canal de controle
~umCanal = Bus.control(s, 1);
~outroCanal = Bus.control(s, 1);
// Iniciar controles
{Out.kr(~umCanal, LFSaw.kr(1).range(100, 1000))}.play;
{Out.kr(~outroCanal, LFSaw.kr(2, mul: -1).range(500, 2000))}.play;
// Toque um nota
x = Synth("simples", [\freq, 800]);
x.set(\freq, ~umCanal.asMap);
x.set(\freq, ~outroCanal.asMap);
x.free;
\end{lstlisting}
