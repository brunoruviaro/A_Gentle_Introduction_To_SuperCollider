\section{Variáveis}
\label{sec:variables}

Você pode guardar números, palavras, unidades geradoras, funções ou blocos inteiros de código em variáveis. Variáveis podem ser letras minúsculas simples ou palavras escolhidas por você. Usamos o sinal de igual (=) para "atribuir" variáveis. Rode estas linhas uma de cada vez e observe a Post window:

 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
x = 10;
y = 660;
y; // confira o que está aqui dentro
x;
x + y;
y - x;
\end{lstlisting}
 

A primeira linha atribui o número 10 à variável \texttt{x}. A segunda linha coloca 660 na variável \texttt{y}. As próximas duas linhas provam que estas letras agora "contêm" estes números (os dados). Finalmente, as duas últimas linhas mostram que podemos usar as variáveis para fazer qualquer operação com os dados guardados nelas.

Letras minúsculas de \texttt{a} a \texttt{z} podem ser utilizadas a qualquer momento como variáveis no SuperCollider. A única letra simples que costuma-se não usar é \texttt{s}, que por convenção é reservada para representar o Servidor. Qualquer coisa pode entrar em uma variável:
 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
a = "Olá, Mundo"; // uma cadeia de caracteres
b = [0, 1, 2, 3, 5]; // uma lista
c = Pbind(\note, Pwhite(0, 10), \dur, 0.1); // você aprenderá tudo sobre Pbind mais adiante, não se preocupe

// ...e agora você pode utilizá-las como você utilizaria os dados originais:
a.postln; // poste-a
b + 100; // faça contas
c.play; // toque aquele Pbind
d = b * 5; // pegue b, multiplique por 5 e atribua o resultado a uma nova variável
\end{lstlisting}

Muitas vezes fará mais sentido dar nomes melhores para suas variáveis, para ajudar a lembrar o que elas representam no seu código. Você pode usar um $\sim$ (til) para declarar uma variável com um nome mais longo. Note que não há espaço entre o til e o nome da variável.

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
~minhasFreqs = [415, 220, 440, 880, 220, 990];
~minhasDurs = [0.1, 0.2, 0.2, 0.5, 0.2, 0.1];

Pbind(\freq, Pseq(~minhasFreqs), \dur, Pseq(~minhasDurs)).play;
\end{lstlisting}
 

Nomes de variáveis devem começar com letras minúsculas depois do til. Você pode usar números, underscores e letras maiúsculas no meio do nome, somente não como primeiro caracter. Todos os caracteres têm de ser contíguos (sem espaços ou pontuação). Resumindo, atenha-se a letras e números e underscores ocasionais, evitando todos os outros caracteres ao nomear suas variáveis. \texttt{$\sim$minhasFreqs}, \texttt{$\sim$aMelhorSenoide} e \texttt{$\sim$banana\_3} são nomes válidos. \texttt{$\sim$MinhasFreqs}, \texttt{$\sim$aMelhor\&*\#\@Senoide} e \texttt{$\sim$banana!!!} vão estragar o seu dia.

Há dois tipos de variáveis que você pode criar: variáveis "globais" e variáveis locais.

\subsection{"Global" vs. Local}

As variáveis que vimos até agora (letras minúsculas de \texttt{a} a \texttt{z} e aquelas começando com o caracter til ($\sim$)) são genericamente chamadas "variáveis globais", porque uma vez declaradas, funcionarão "globalmente" em qualquer lugar no patch, em outros patches e mesmo em outros documentos do SC, até que você saia do SuperCollider.\footnote{Tecnicamente falando, variáveis começando com um til são chamadas variáveis de Ambiente ("Environment") e variáveis de letras minúsculas (de a a z) são chamadas variáveis do Interpretador. Iniciantes no SuperCollider não precisam se preocupar com estas distinções, mas mantenha-as em mente para o futuro. O Capítulo 5 do The SuperCollider Book explica a diferença em detalhes.}

Variáveis locais, por outro lado, são declaradas com a palavra-chave específica \texttt{\textbf{var}} no início de uma linha. Você pode atribuir um valor inicial para uma variável no momento de sua declaração (\texttt{\textbf{var} tangerina = 4}). Variáveis locais apenas existem dentro do escopo de seu bloco de código.

Aqui está um exemplo simples comparando os dois tipos de variáveis. Execute linha a linha e observe a Post window.

 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Variáveis de Ambiente
~abacates = 4;
~laranjas = 5;
~tangerinas = 2;
~bananas = 1;

["Cítricos", ~laranjas + ~tangerinas];
["Não-Cítricos", ~bananas + ~abacates];

// Variáveis locais: válidas apenas dentro do bloco de código.
// Execute o bloco uma vez e observe o Post window:
(
var jacas = 4, laranjas = 3, tangerinas = 8, bananas = 10;
["Frutas cítricas", laranjas + tangerinas].postln;
["Frutas Não-cítricas", bananas + jacas].postln;
"Fim".postln;
)

~abacates; // variável ainda existe
jacas; // variável não existe mais
\end{lstlisting}

\subsection{Reatribuição}

Uma última coisa útil de se entender sobre variáveis é que elas podem ser \emph{reatribuídas}: você pode dar-lhes um novo valor a qualquer momento.

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Atribua uma variável
a = 10 + 3;
a.postln; // verifique
a = 999; // reatribua a variável (dê-lhe um novo valor)
a.postln; // verifique: o valor antigo se foi.
\end{lstlisting}

Uma prática muito comum que pode parecer um pouco confusa para iniciantes é quando \emph{a própria variável é usada na sua própria reatribuição}. Dê uma olhada neste exemplo:

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
x = 10; // atribua 10 à variável x
x = x + 1; // atribua x + 1 à variável x
x.postln; // verifique
\end{lstlisting}

A maneira mais fácil de entender essa última linha é lê-la da seguinte forma: "pegue o valor atual da variável x, adicione 1 a ela e atribua este novo resultado à variável x". No fundo, não é tão complicado e você verá mais tarde como isso pode ser útil.\footnote{Este exemplo claramente demonstra que o sinal de igual, em programação, não é o mesmo sinal de igual que você aprendeu em matemática. Em matemática, $x = x + 1$ é impossível (um número não pode ser igual a si mesmo mais um). Já em uma linguagem de programação como o SuperCollider, o sinal de igual pode ser entendido como um tipo de ação: \emph{pegue o resultado da expressão à direita do símbolo e o "atribua" à variável ao lado esquerdo}.}

