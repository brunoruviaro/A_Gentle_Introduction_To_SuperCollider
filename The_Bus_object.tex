\section{O objeto Bus}
\label{sec:busobject}

Aqui está um exemplo que utiliza tudo o que você aprendeu nas duas seções anteriores: canais de áudio e expansão multicanal.
 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Rode isso primeiro ('ligar reverb' -- inicialmente você não vai ouvir nada)
r = {Out.ar(0, FreeVerb.ar(In.ar(55, 2), mix: 0.5, room: 0.9, mul: 0.4))}.play;

// Agora rode isto em seguida ('envie o som para o canal de reverb')
a = {Out.ar(55, SinOsc.ar([800, 880], mul: LFPulse.ar(2)))}.play;
a.free;
\end{lstlisting}
 

Graças à expansão multicanal, o som usa dois canais. Quando (no sintetizador \texttt{a}) enviamos o sinal para o canal 55, na verdade, dois canais estão sendo utilizados---o número 55 e o canal imediatamente adjacente, 56. No reverb (sintetizador \texttt{r}), indicamos com \texttt{In.ar(55, 2)} que queremos ler 2 canais, começando pelo canal 55, de modo que tanto 55 quanto 56 vão entrar no reverb. A saída do reverb também é expandida para dois canais, de maneira que o sintetizador \texttt{r} manda som para os canais 0 e 1 (canais esquerdo e direito da sua placa de som).

Esta escolha de canal (número 55) para conectar uma fonte sonora a um efeito foi arbitrária: poderia ter sido qualquer outro número entre 16 e 127 (lembre-se, canis 0-15 são reservados para a as saídas e entradas da placa de som). Seria muito inconveniente ter que ficar escolhendo e lembrando de números assim a todo momento. Quando os patches começarem a ficar mais complexos, imagine que pesadelo: "Qual foi mesmo o canal que escolhi para o reverb? Era 59 ou 95? E o canal do meu delay? Será que era 27? Não lembro..." e assim por diante.

O SuperCollider toma conta disso para você com objetos Bus. Nos exemplos acima, nós só definimos manualmente o tal do canal 55 como forma de demonstração. No dia-a-dia com o SuperCollider, você pode simplesmente usar o objeto Bus. O objeto Bus faz o trabalho de escolher um canal disponível para você e monitorá-lo. Eis como usá-lo:

 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Criar o bus
~meuBus = Bus.audio(s, 2);
// Ligar o reverb: ler de ~meuBus (fonte sonora)
r = {Out.ar(0, FreeVerb.ar(In.ar(~meuBus, 2), mix: 0.5, room: 0.9, mul: 0.4))}.play;
// Alimente o ~meuBus com o som
b = {Out.ar(~meuBus, SinOsc.ar([800, 880], mul: LFPulse.ar(2)))}.play;
// Libere ambos os sintetizadores
r.free; b.free;
\end{lstlisting}
 

O primeiro argumento do \texttt{Bus.audio} é a variável \texttt{s}, que representa o servidor. O segundo argumento é quantos canais você precisa (2 no exemplo). Daí você armazena isso em uma variável com um nome qualquer (\texttt{$\sim$meuBus} no exemplo, mas poderia ser \texttt{$\sim$reverbBus}, \texttt{$\sim$fonte}, \texttt{$\sim$tangerina} ou o que fizer sentido para você dentro do seu patch). Depois disso, sempre que você precisar se referir àquele bus, é só usar a variável que você criou.
