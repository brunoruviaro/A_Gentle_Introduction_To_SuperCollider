\section{Mais truques com Patterns}

\subsection{Acordes}

Quer escrever acordes dentro de \texttt{Pbind}s? Escreva-os como listas (valores separados por vírgula entre colchetes):
 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
(
Pbind(
	\note, Pseq([[0, 3, 7], [2, 5, 8], [3, 7, 10], [5, 8, 12]], 3),
	\dur, 0.15
).play;
)
// Diversão com "strum" ("dedilhar")
(
Pbind(
	\note, Pseq([[-7, 3, 7, 10], [0, 3, 5, 8]], 2),
	\dur, 1,
	\legato, 0.4,
	\strum, 0.1 // try 0, 0.1, 0.2, etc
).play;
)
\end{lstlisting}
 

\subsection{Escalas}

Quando estiver usando \texttt{\textbackslash degree} ("grau") para especificar alturas, pode-se acrescentar uma linha com a palavra-chave \texttt{\textbackslash scale} ("escala") para mudar escalas (nota: isso só funciona em conjunção com \texttt{\textbackslash degree}, não com \texttt{\textbackslash note}, \texttt{\textbackslash midinote} ou \texttt{\textbackslash freq}):

 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
(
Pbind(
	\scale, Scale.harmonicMinor,
	\degree, Pseq([0, 1, 2, 3, 4, 5, 6, 7], 1),
	\dur, 0.15;
).play;
)

// Execute esta linha para ver uma lista de todas as escalas disponíveis:
Scale.directory;

// Se você precisa de uma nota cromática entre dois graus da escala, faça assim:
(
Pbind(
	\degree, Pseq([0, 1, 2, 3, 3.1, 4], 1),
).play;
)

// O 3.1 acima quer dizer um passo cromático sobre o grau 3 da escala (neste caso, fá# acima do fá). Note que quando você não especifica explicitamente uma escala \scale, Scale.major é subentendida.
\end{lstlisting}

\subsection{Transposition}

Use a palavra-chave \texttt{\textbackslash ctranspose} para realizar uma transposição cromática. Isso funciona em conjunto com \texttt{\textbackslash degree}, \texttt{\textbackslash note} e \texttt{\textbackslash midinote}, mas não com \texttt{\textbackslash freq}.

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
(
Pbind(
	\note, Pser([0, 2, 3, 5, 7, 8, 11, 12], 11),
	\ctranspose, 12, // transpor oitava acima (= 12 semitons)
	\dur, 0.15;
).play;
)
\end{lstlisting}

\subsection{Microtons}
 
Como escrever microtons:

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Microtons com \note e \midinote:
Pbind(\note, Pseq([0, 0.5, 1, 1.5, 1.75, 2], 1)).play;
Pbind(\midinote, Pseq([60, 69, 68.5, 60.25, 70], 1)).play;
\end{lstlisting}
 
\subsection{Andamento}

Os valores que você fornece para a palavra-chave \texttt{\textbackslash dur} de um Pbind estão em \emph{número de tempos}, isto é, 1 significa um tempo, 0.5 significa meio tempo e assim por diate. A não ser que você especifique outra coisa, o tempo padrão é 60 BPM (batidas por minuto). Para tocar em um andamento diferente, simplesmente crie um novo TempoClock. Aqui está um \texttt{Pbind} tocando a 120 batidas por minuto (BPM):
 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
(
Pbind(\degree, Pseq([0, 0.1, 1, 2, 3, 4, 5, 6, 7]),
	\dur, 1;
).play(TempoClock(120/60)); // 120 batidas em 60 segundos: 120 BPM
)
\end{lstlisting}
 
Por acaso, você percebeu que o \texttt{Pseq} recebe um único argumento (a lista)? Onde está o valor \texttt{repeats} que sempre veio após a lista? Você pode ouvir que o exemplo toca a sequência apenas uma vez, mas por que? Está é uma propriedade comum a todos os Patterns (e também de muitos outros objetos dentro do SuperCollider): se você omite um argumento, será utilizado um valor padrão que já vem embutido. Neste caso, o \texttt{repeats} padrão para \texttt{Pseq} é 1. Lembra-se do seu primeiro \texttt{Pbind} ridiculamente simples? Era um mero \texttt{Pbind(\textbackslash degree, 0).play} e ele somente sabia tocar uma nota. Você não forneceu nenhuma informação para duração, amplitude, legato, etc. Nestes casos o \texttt{Pbind} simplesmente segue em frente e usa os valores padrão.


\subsection{Pausas}

É assim que se escreve pausas. O número dentro de \texttt{Rest} é a duração da pausa em tempos. Pausas podem estar em qualquer lugar no Pbind, não somente na linha \texttt{\textbackslash dur}.

 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
(
Pbind(
	\degree, Pwhite(0, 10),
	\dur, Pseq([0.1, 0.1, 0.3, 0.6, Rest(0.3), 0.25], inf);
).play;
)
\end{lstlisting}
 

\subsection{Tocando dois ou mais Pbinds juntos}

Para disparar alguns Pbinds simultaneamente, simplesmente empacote todos eles em um único bloco de código:
 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
( // abrindo o grande bloco
Pbind(
	\freq, Pn(Pseries(110, 111, 10)),
	\dur, 1/2,
	\legato, Pwhite(0.1, 1)
).play;

Pbind(
	\freq, Pn(Pseries(220, 222, 10)),
	\dur, 1/4,
	\legato, Pwhite(0.1, 1)
).play;

Pbind(
	\freq, Pn(Pseries(330, 333, 10)),
	\dur, 1/6,
	\legato, 0.1
).play;
) // fechando o grande bloco
\end{lstlisting}

Para tocar Pbinds de uma maneira temporalmente ordenada (em vez de simplesmente executá-los manualmente um após o outro), você pode usar \texttt{\{ \}.fork} [N.T.: "garfo", uma maneira abreviada de criar rotinas de programação]:

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Exemplo básico de fork. Observe o Post window:
( 
{
	"uma coisa".postln;
	2.wait;
	"outra coisa".postln;
	1.5.wait;
	"uma última coisa".postln;
}.fork;
)
// Um exemplo mais interessante:
(
t = TempoClock(76/60);
{
	Pbind(
		\note, Pseq([[4, 11], [6, 9]], 32),
		\dur, 1/6,
		\amp, Pseq([0.05, 0.03], inf)
	).play(t);
	
	2.wait;
	
	Pbind(
		\note, Pseq([[-25, -13, -1], [-20, -8, 4], \rest], 3),
		\dur, Pseq([1, 1, Rest(1)], inf),
		\amp, 0.1,
		\legato, Pseq([0.4, 0.7, \rest], inf)
	).play(t);

	2.75.wait;
	
	Pbind(
		\note, Pseq([23, 21, 25, 23, 21, 20, 18, 16, 20, 21, 23, 21], inf),
		\dur, Pseq([0.25, 0.75, 0.25, 1.75, 0.125, 0.125, 0.80, 0.20, 0.125, 0.125, 1], 1),
		\amp, 0.1,
		\legato, 0.5
	).play(t);
}.fork(t);
)
\end{lstlisting}
 
Para formas avançadas de tocar \texttt{Pbins}s simultaneamente e em sequência, confira \texttt{Ppar} e \texttt{Pspawner}. Para saber mais sobre \texttt{fork}, veja o arquivo de Ajuda de  \texttt{Routine}.

\subsection{Usando variáveis}

Na seção anterior, "Expandindo seu vocabulário de Patterns", você percebeu quantas vezes teve de digitar a mesma lista de notas [0, 2, 3, 5, 7, 8, 11, 12] diversas vezes para múltiplos \texttt{Pbind}s? Não é muito eficiente ficar copiando a mesma coisa à mão, certo? Em programação sempre que você se vir fazendo a mesma tarefa repetidas vezes, é provavelmente hora de adotar uma estratégia mais inteligente de chegar ao mesmo objetivo. Neste caso, podemos usar variáveis. Como você deve se lembrar, variáveis permitem que você se refira a qualquer conjunto de dados de uma maneira flexível e concisa (releia a seção \ref{sec:variables} se necessário). Aqui está um exemplo:

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Usando muito a mesma sequência de números? Guarde-a em uma variável:
c = [0, 2, 3, 5, 7, 8, 11, 12];

// Agora você pode simplesmente se referir a elax
Pbind(\note, Pseq(c, 1), \dur, 0.15).play;
Pbind(\note, Prand(c, 6), \dur, 0.15).play;
Pbind(\note, Pslide(c, 5, 3, 1), \dur, 0.15).play;
\end{lstlisting}
 
Outro exemplo para praticar o uso de variáveis: digamos que queremos tocar dois \texttt{Pbind}s simultaneamente. Uma delas faz uma escala maior ascendente e a outra faz uma escala maior descedente uma oitava acima. Ambas usam a mesma lista de durações. Eis um jeito de escrever isso:

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
~escala = [0, 1, 2, 3, 4, 5, 6, 7];
~durs = [0.4, 0.2, 0.2, 0.4, 0.8, 0.2, 0.2, 0.2];
(
Pbind(
	\degree, Pseq(~escala),
	\dur, Pseq(~durs)
).play;

Pbind(
	\degree, Pseq(~escala.reverse + 7),
	\dur, Pseq(~durs)
).play;
)
\end{lstlisting}
 
Alguns truques interessantes: graças às variáveis, reutilizamos a mesma lista de graus de escala e durações para ambos os \texttt{Pbind}s. Quisemos que a segunda escala fosse descendente e uma oitava acima da primeira. Para obter isso, simplesmente utilizamos a mensagem \texttt{.reverse} para inverter a ordem da lista (digite \texttt{$\sim$scale.reverse} em um nova linha e execute para ver exatamente o que ela faz). Depois, adicionamos 7 para transpô-la uma oitava acima (teste isso também para ver o resultado).\footnote{Poderíamos também ter usado \texttt{\textbackslash ctranspose, 12} para obter a mesma transposição.} Tocamos dois \texttt{Pbind}s ao mesmo tempo fechando-os no mesmo bloco de código.

Exercício: crie um \texttt{Pbind} adicional dentro do código acima, para que você ouça três vozes simultâneas. Use ambas as variáveis (\texttt{$\sim$scale} e \texttt{$\sim$durs}) de alguma maneira diferente---por exemplo, use-a dentro de um Pattern diferente de Pseq; mude o intervalo de transposição; inverta e/ou multiplique as durações; etc.
