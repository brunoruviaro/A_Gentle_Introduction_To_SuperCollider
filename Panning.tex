\section{Pan}

Panorâmica ou Pan é a distribuição de um sinal de áudio em um campo sonoro estéreo ou multicanal. Aqui temos um sinal mono pulando entre o canal esquerdo e o direito graças ao \texttt{Pan2}:\footnote{Para pan multicanal, dê uma olhada em \texttt{Pan4} e \texttt{PanAz}. Usuários avançados podem querer dar uma olhada em plug-ins de SuperCollider para Ambisonics.}
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
p = {Pan2.ar(in: PinkNoise.ar, pos: SinOsc.kr(2), level: 0.1)}.play;
p.free;
\end{lstlisting}
No arquivo de Ajuda do \texttt{Pan2}, você pode ver que o argumento \texttt{pos} (“posição”) espera um número entre -1 (esquerda) e +1 (direita), 0 sendo o centro. É por isso que você pode utilizar um \texttt{SinOsc} diretamente neste argumento: o oscilador senoidal é uma UGen bipolar, então ela gera números entre -1 e +1 por definição.

Aqui há um exemplo mais elaborado. Uma onda dente-de-serra passa por um filtro passa banda muito estreito (\texttt{rq: 0.01}). Note o uso de variáveis locais para tornar modulares diferentes partes do código. Analise e tente entender o máximo que você puder no exemplo acima. Depois responda as perguntas abaixo.

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
(
x = {
	var lfn = LFNoise2.kr(1);
	var saw = Saw.ar(
		freq: 30, 
		mul: LFPulse.kr(
			freq: LFNoise1.kr(1).range(1, 10),
			width: 0.1));
	var bpf = BPF.ar(in: saw, freq: lfn.range(500, 2500), rq: 0.01, mul: 20);
	Pan2.ar(in: bpf, pos: lfn);
}.play;
)
x.free;
\end{lstlisting}
 
Perguntas:
\begin{enumerate}[(a)]
\item A variável \texttt{lfn} é usada em dois lugares diferentes. Por quê? (Qual o resultado?)
\item O que acontece se você mudar o argumento \texttt{mul:} do \texttt{BPF} de 20 para 10, 5 ou 1? Por que um número grande como 20 foi usado?
\item Qual parte do código está controlando o ritmo?
\end{enumerate}

Respostas ao final deste documento.\endnote{(a) A variável \texttt{lfn} simplesmente armazena um \texttt{LFNoise2}. O papel do \texttt{LFNoise2} na vida é gerar aleatoriamente um novo número (entre -1 e +1) a cada segundo e deslizar até ele a partir do último número aleatório (diferentemente do \texttt{LFNoise0}, que salta para o novo número imediatamente). O primeiro uso desta variável \texttt{lfn} é no argumento \texttt{freq} do BPF: \texttt{lfn.range(500, 2500)}. Isso pega os números entre -1 e +1 e os escalona para o âmbito 500-2500. Estes números são então usados como a frequência central do filtro. Estas frequências são as alturas que escutamos deslizando para cima e para baixo. Finalmente, \texttt{lfn} é usada novamente para controlar a posição do pan \texttt{Pan2}. Ela é usada diretamente (sem uma mensagem \texttt{.range}) porque os números já estão no âmbito que queremos (de -1 a +1). O resultado interessante disso é que associamos a mudança de frequência com a mudança de posição. Como? A cada segundo, \texttt{LFNoise2} começa a deslizar em direção a um novo número aleatório e isso se torna uma mudança sincronizada na frequência do filtro e na posição de pan. Se tivéssemos dois \texttt{LFNoise2} diferentes, um em cada lugar, as mudanças não teriam relação entre si (o que poderia ser bom também, mas o resultado sonoro é distinto). \\
(b) Um \texttt{mul:} de 1 soaria simplesmente suave demais. Como o filtro é bastante estreito, ele retira tanto do sinal original que a amplitude sofre uma queda exagerada. Precisamos então aumentar o sinal de volta para um âmbito razoavelmente audível, então é por isso que temos \texttt{mul: 20} ao final da linha do \texttt{BPF}. \\
(c) O ritmo é controlado pelo \texttt{LFPulse} que é o argumetno \texttt{mul:} do \texttt{Saw}. A frequência do \texttt{LFPulse} (quantos pulsos por segundo) é controlada por um \texttt{LFNoise1} que produz números de 1 a 10 (interpolando entre eles). Tais números são as “quantas notas por segundo” deste patch.
}
