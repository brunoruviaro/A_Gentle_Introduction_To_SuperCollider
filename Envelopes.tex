\section{Envelopes}

Até agora, a maioria dos nossos exemplos foi de sons contínuos. Já está na hora de aprender a modelar o envelope de amplitude de um som. Um bom exemplo para começar é um envelope percussivo. Imagine um ataque em um prato suspenso. O tempo que o som leva para ir do silêncio à amplitude máxima é muito curto---alguns milissegundos talvez. Isto é chamado \emph{tempo de ataque}. O tempo que leva para o som do prato diminuir da máxima amplitude de volta ao silêncio (zero) é um pouco mais longo, talvez alguns segundos. Isto é chamado o \emph{tempo de repouso}.

Pense em um envelope de amplitude simplesmente como um número que muda ao longo do tempo e pode ser utilizado como o multiplicador (\texttt{mul}) de qualquer UGen que produz som. Estes números devem estar entre 0 (silêncio) e 1 (amplitude máxima), porque é assim que o SuperCollider entende a amplitude. Talvez agora você se dê conta de que o último exemplo já continha um envelope de amplitude: em \texttt{\{WhiteNoise.ar(Line.kr(0.2, 0, 2, doneAction: 2))\}.play}, fazemos a amplitude do ruído branco ir de 0.2 a 0 em 2 segundos. Um \texttt{Line.kr}, no entanto, não é um tipo de envelope muito flexível.

\texttt{Env} é o objeto que você usará o tempo todo para definir vários tipos de envelopes. O \texttt{Env} tem muitos métodos úteis; só conseguiremos ver alguns deles aqui. Dê uma olhada na Ajuda do \texttt{Env} para aprender mais. 

\subsection{Env.perc}

\texttt{Env.perc} é uma maneira prática de se obter um envelope percussivo. Ele aceita quatro argumentos:  attackTime, releaseTime, level e curve (que podemos traduzir como: "tempo de ataque, tempo de repouso, nível e curva". No caso de envelopes de amplitude, "nível" é como se fosse o "volume máximo" que o seu envelope pode alcançar). Vejamos algumas formas típicas, ainda fora de qualquer sintetizador.

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
Env.perc.plot; // usando todos os argumentos padrão
Env.perc(0.5).plot; // attackTime: 0.5
Env.perc(attackTime: 0.3, releaseTime: 2, level: 0.4).plot;
Env.perc(0.3, 2, 0.4, 0).plot; // o mesmo que acima, mas curve:0 produz uma linha reta
\end{lstlisting}
 
Agora simplesmente o encaixamos dentro de um sintetizador:

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
{PinkNoise.ar(Env.perc.kr(doneAction: 2))}.play; // argumentos padrão do Env.perc
{PinkNoise.ar(Env.perc(0.5).kr(doneAction: 2))}.play; 
{PinkNoise.ar(Env.perc(0.3, 2, 0.4).kr(2))}.play;
{PinkNoise.ar(Env.perc(0.3, 2, 0.4, 0).kr(2))}.play;
\end{lstlisting}
 
Tudo o que você tem de fazer é adicionar \texttt{.kr(doneAction: 2)} logo depois de \texttt{Env.perc} e pronto. Na verdade, neste caso você pode até remover a declaração explícita do argumento doneAction e simplesmente ficar com \texttt{.kr(2)}. O \texttt{.kr} está esta dizendo para o SC rodar este envelope na velocidade da taxa de controle (como outros sinais de controle que vimos antes).

\subsection{Env.triangle}

\texttt{Env.triangle} recebe apenas dois argumentos: duração e nível de amplitude. Exemplos:

 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Veja-o:
Env.triangle.plot;
// Ouça-o:
{SinOsc.ar([440, 442], mul: Env.triangle.kr(2))}.play;
// Aliás, um envelope pode ser um multiplicador em qualquer lugar do seu código:
{SinOsc.ar([440, 442]) * Env.triangle.kr(2)}.play;
\end{lstlisting}

\subsection{Env.linen}

\texttt{Env.linen} descreve um envelope linear com ataque, porção de sustentação e repouso. Você também pode especificar o nível de amplitude e tipo de curva. Exemplo:

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Veja-o:
Env.linen.plot;
// Ouça-o:
{SinOsc.ar([300, 350], mul: Env.linen(0.01, 2, 1, 0.2).kr(2))}.play;
\end{lstlisting}

\subsection{Env.pairs}

Quer algo ainda mais flexível? Com \texttt{Env.pairs} você pode ter envelopes com qualquer forma e duração que quiser. \texttt{Env.pairs} recebe dois argumentos: uma lista (array) de pares de [tempo, nível] e um tipo de curva (veja na Ajuda de Env todos os tipos de curva disponíveis).

 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
(
{
	var env = Env.pairs([[0, 0], [0.4, 1], [1, 0.2], [1.1, 0.5], [2, 0]], \lin);
	env.plot;
	SinOsc.ar([440, 442], mul: env.kr(2));
}.play;
)
\end{lstlisting}
 
Leia a array de pares assim:
\begin{center}
No tempo 0, esteja no nível 0;\\
No tempo 0.4, esteja no nível 1;\\
No tempo 1, esteja no nível 0.2;\\
No tempo 1.1, esteja no nível 0.5;\\
No tempo 2, esteja no nível 0;
\end{center}

\subsubsection{Envelopes---não só para amplitude}

Nada impede você de usar as estas mesmas formas para controlar algo que não seja amplitude do som. Você só precisa redimensioná-las para o âmbito de números desejado. Por exemplo, Você pode criar um envelope para controlar a mudança de frequências ao longo do tempo:

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
(
{
	var freqEnv = Env.pairs([[0, 100], [0.4, 1000], [0.9, 400], [1.1, 555], [2, 440]], \lin);
	SinOsc.ar(freqEnv.kr, mul: 0.2);
}.play;
)
\end{lstlisting}

Envelopes são uma maneira poderosa de controlar qualquer parâmetro de um sintetizador que precisa variar ao longo do tempo.

\subsection{Envelope ADSR}

Todos os envelopes vistos até agora têm uma coisa em comum: eles têm uma duração fixa, pré-definida. Há situações, no entanto, em que este tipo de envelope não é adequado. Por exemplo, imagine que você está tocando em um teclado MIDI. O \textit{ataque} da nota é disparado quando você pressiona a tecla. O \textit{repouso}, quando você tira seu dedo da tecla. Mas a quantidade de tempo que você permanece com o dedo pressionando a tecla não é conhecido de antemão. O que precisamos neste caso é de um "envelope sustentado". Em outras palavras, depois da porção de ataque, o envelope precisa segurar a nota por uma quantidade de tempo indefinida e apenas disparar a porção de repouso depois de algum sinal, ou mensagem--- isto é, o momento em que você "solta a tecla".

Um envelope ASR (Ataque, Sustentação, Repouso) se encaixa perfeitamente nesse caso. Uma variação mais popular é o envelope ADSR (Ataque, Decaimento, Sustentação, Repouso). Vamos dar uma olhada nos dois.

 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// ASR
// Toque nota ('aperte tecla')
// attackTime: 0.5 seconds, sustainLevel: 0.8, releaseTime: 3 seconds
x = {arg gate = 1, freq = 440; SinOsc.ar(freq: freq, mul: Env.asr(0.5, 0.8, 3).kr(doneAction: 2, gate: gate))}.play;
// Pare nota ('tirar dedo da tecla' - ativar a porção de repouso)
x.set(\gate, 0); // uma alternativa é x.release

// ADSR (ataque, decaimento, sutentação, repouso)
// Toque nota:
(
d = {arg gate = 1;
	var snd, env;
	env = Env.adsr(0.01, 0.4, 0.7, 2);
	snd = Splay.ar(BPF.ar(Saw.ar((32.1, 32.2..33)), LFNoise2.kr(12).range(100, 1000), 0.05, 10));
	Out.ar(0, snd * env.kr(doneAction: 2, gate: gate));
}.play;
)
// Pare nota:
d.release; // isto é equivalente a d.set(\gate, 0);
\end{lstlisting}
 
Conceitos-chave:

\begin{description}
\item[Ataque ("Attack")] O tempo (em segundos) que leva para ir do silêncio (zero) até o pico de amplitude
\item[Decaimento ("Decay")] O tempo (em segundos) que leva para ir do pico de amplitude para a amplitude de sustentação
\item[Sustentação ("Sustain")] A amplitude (entre 0 e 1) na qual a nota é sustentada (importante: isto não tem nada a ver com tempo)
\item[Repouso ("Release")] O tempo (em segundos) que leva para ir do nível de sustentação para o zero (silêncio).
\end{description}

Como envelopes sustentados não tem uma duração total conhecida de antemão, eles precisam de uma notificação tanto de quando começar (disparar o ataque) e quando parar (disparar o repouso). Esta notificação é chamada um \emph{gate} ("portão"). O gate é o que diz para que o envelope se ‘abra’ (1) ou se ‘feche’ (0), portanto começando e terminando a nota.

Para que um envelope ASR ou ADSR funcione no seu sintetizador, você precisa declarar um argumento \texttt{gate}. Normalmente, o padrão é \texttt{gate = 1} porque você quer que o sintetizador comece a tocar assim que for instanciado (ativado). Quando você quer que o sintetizador pare, simplesmente mande uma mensagem \texttt{.release} ou \texttt{.set(\textbackslash gate, 0)}: a porção de repouso do envelope será então disparada. Por exemplo, se seu tempo de repouso é 3, a nota vai levar três segundos para se extinguir completamente \emph{a partir do momento em que você enviou a mensagem} \texttt{.set(\textbackslash gate, 0)}. 

\subsection{EnvGen}

Vale registrar que a construção que você aprendeu nesta seção para gerar envelopes é um atalho, como mostrado no código abaixo.

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Isso:
{ SinOsc.ar * Env.perc.kr(doneAction: 2) }.play;
// ... é um atalho disso:
{ SinOsc.ar * EnvGen.kr(Env.perc, doneAction: 2) }.play;
\end{lstlisting}

\texttt{EnvGen} é a UGen que de fato toca os envelopes segmentados ("breakpoint envelopes") definidos por \texttt{Env}. Para todos os propósitos práticos, você pode continuar a usar o atalho. Mas é útil saber que estas notações são equivalentes, já que você muitas vezes verá  \texttt{EnvGen} sendo utilizado nos arquivos de Ajuda e outros exemplos online.
