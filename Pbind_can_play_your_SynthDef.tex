\section{Pbind pode tocar sua SynthDef}

Uma das belezas de se criar seus sintetizadores como \texttt{SynthDef}s é que você pode usar \texttt{Pbind} para tocá-los.

Assumindo que a SynthDef \texttt{"uau"} ainda esteja armazenado na memória (deveria estar, a não ser que você tenha fechado e reaberto o SC depois do último exemplo), experimente os \texttt{Pbind}s abaixo:

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
(
Pbind(
	\instrument, "uau",
	\degree, Pwhite(-7, 7),
	\dur, Prand([0.125, 0.25], inf),
	\amp, Pwhite(0.5, 1),
	\uaurelease, 1
).play;
)

(
Pbind(
	\instrument, "uau",
	\scale, Pstutter(8, Pseq([
		Scale.lydian,
		Scale.major,
		Scale.mixolydian,
		Scale.minor,
		Scale.phrygian], inf)),
	\degree, Pseq([0, 1, 2, 3, 4, 5, 6, 7], inf),
	\dur, 0.2,
	\amp, Pwhite(0.5, 1),
	\uaurelease, 4,
	\legato, 0.1
).play;
)
\end{lstlisting}
 
Ao usar \texttt{Pbind} para tocar um das suas \texttt{SynthDef}s personalizados, esteja atento aos seguintes pontos:

\begin{itemize}
\item Use a chave ("key") \texttt{\textbackslash instrument} do \texttt{Pbind} para declarar o nome da sua \texttt{SynthDef}.
\item Todos os argumentos da sua SynthDef são controláveis a partir do \texttt{Pbind}: simplesmente use-os como chaves do \texttt{Pbind}. Por exemplo, repare no argumento chamado \texttt{\textbackslash uaurelease} utilizado acima. Esta não é uma das chaves padrão entendidas pelo \texttt{Pbind}---ela só existe na definição do sintetizador  \texttt{uau} (o nome bobo foi escolhido de propósito).

\item Para utilizar todas as facildades de conversão de alturas do \texttt{Pbind} (as chaves \texttt{\textbackslash degree}, \texttt{\textbackslash note} e \texttt{\textbackslash midinote}), tenha certeza de que sua \texttt{SynthDef} tem um argumento de entrada \texttt{freq} (tem que ser escrito exatamente assim). Pbind fará os cálculos para você.
\item Se for usar um envelope sustentado como \texttt{Env.adsr}, garanta que seu sintetizador tenha o argumento padrão \texttt{gate = 1} (\texttt{gate} tem que ser excrito exatamente assim, porque o \texttt{Pbind} o utiliza nos bastidores para parar as notas nos momentos certos).
\item Se você não estiver usando um envelope sustentado, tenha certeza que sua SynthDef inclui um doneAction: 2 em uma UGen apropriada, para liberar automaticamente os nós de sintetizador no servidor.
\end{itemize}

Exercício: escreva um ou mais \texttt{Pbind}s para tocar a SynthDef \texttt{"pluck"} fornecida abaixo. Para o argumento \texttt{cordaAbafada}, tente valores entre  0.1 e 0.9. Faça com que seu \texttt{Pbind} toque uma sequência lenta de acordes. Tente arpejar os acordes com \texttt{\textbackslash strum}.

\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
(
SynthDef("pluck", {arg amp = 0.1, freq = 440, decaimento = 5, cordaAbafada = 0.1;
var env, som;
env = Env.linen(0, decaimento, 0).kr(doneAction: 2);
som = Pluck.ar(
        in: WhiteNoise.ar(amp),
        trig: Impulse.kr(0),
        maxdelaytime: 0.1,
        delaytime: freq.reciprocal,
        decaytime: decaimento,
        coef: cordaAbafada);
    Out.ar(0, [som, som]);
}).add;
)
\end{lstlisting}
