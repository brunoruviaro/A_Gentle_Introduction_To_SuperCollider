\section{Nós de sintetizador}

Nos exemplos anteriores com \texttt{PlayBuf}, você teve que apertar [ctrl+.] depois de cada linha para parar o som. Em outros exemplos, você atribuiu o sintetizador a uma variável (como \texttt{x = \{WhiteNoise.ar\}.play}) para que você pudesse pará-lo diretamente com \texttt{x.free}.

Toda vez que você cria um sintetizador no SuperCollider, você sabe que ele roda no servidor, nosso “motor sonoro”. Cada sintetizador que está rodando no servidor é representado por um \emph{node} (“nó"). Podemos dar uma espiada nesta árvore de nós com o comando \texttt{s.plotTree}. Experimente. Uma janela chamada \texttt{NodeTree} (“Árvore de nós”) vai abrir.

 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// abra a GUI
s.plotTree;
// rode estes um por um (não pare o som) e observe a Node Tree:
w = { SinOsc.ar(60.midicps, 0, 0.1) }.play;
x = { SinOsc.ar(64.midicps, 0, 0.1) }.play;
y = { SinOsc.ar(67.midicps, 0, 0.1) }.play;
z = { SinOsc.ar(71.midicps, 0, 0.1) }.play;
w.free;
x.free;
y.free;
z.free;
\end{lstlisting}
 

Cada retângulo que você vê na Node Tree é um nó de sintetizador. Cada sintetizador ganha um nome temporário (algo como temp\_101, temp\_102, etc.) e fica ali enquanto estiver rodando. Experimente agora tocar novamente as quatro senoides e aperte [ctrl+.] (observe a janela Node Tree). O atalho [ctrl+.] impiedosamente interrompe todos os nós que estão rodando no servidor. Por outro lado, com o método \texttt{.free}, você pode ser mais sutil e liberar nós específicos, um de cada vez.

Uma coisa importante de se perceber é que sintetizadores podem continuar a rodar no servidor mesmo que eles estejam gerando apenas silêncio. Eis um exemplo. A amplitude desta UGen \texttt{WhiteNoise} irá de 0.2 a 0 em dois segundos. Depois disso, não escutaremos nada. Mas você vê que o nó do sintetizador ainda esta ali e não desaparecerá até que você o libere.

 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// Execute e observe a janela Node Tree window por alguns segundos
x = {WhiteNoise.ar(Line.kr(0.2, 0, 2))}.play;
x.free;
\end{lstlisting}
 

\subsection{O glorioso doneAction: 2}

Felizmente, há uma maneira de fazer sintetizadores mais espertos neste sentido: por exemplo, não seria ótimo se pudéssemos pedir ao \texttt{Line.kr} para notificar o sintetizador quando ele tiver terminado seu trabalho (a rampa de 0.2 a 0), diante do que o sintetizador se liberasse automaticamente?


Insira o argumento \texttt{doneAction: 2} para resolver todos os nossos problemas.

Toque os exemplos abaixo e compare como eles se comportam com e sem doneAction: 2. Continue observando a Node Tree enquanto você roda as linhas.
 
\begin{lstlisting}[style=SuperCollider-IDE, basicstyle=\scttfamily\footnotesize]
// sem doneAction: 2
{WhiteNoise.ar(Line.kr(0.2, 0, 2))}.play;
{PlayBuf.ar(1, ~buf1)}.play; // PS. isso presume que você anda tem seu arquivo de som carregado no ~buf1 da seção anterior

// com doneAction: 2
{WhiteNoise.ar(Line.kr(0.2, 0, 2, doneAction: 2))}.play;
{PlayBuf.ar(1, ~buf1, doneAction: 2)}.play;
\end{lstlisting}
 
Os sintetizadores com doneAction: 2 vão se liberar automaticamente logo que seu trabalho estiver feito (isto é, assim que a rampa do \texttt{Line.kr} tiver terminado no primeiro exemplo e logo que o \texttt{PlayBuf.ar} tiver terminado de tocar o arquivo de som no segundo exemplo). Saber tudo isso será bastante útil para a próxima seção: Envelopes.
